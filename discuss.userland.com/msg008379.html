<html>	<head>		<title>Offloading email sending</title>		</head>	<body>		<blockquote><blockquote>			<b>Archive of UserLand's first discussion group, started October 5, 1998.</b><hr>			<h2>Offloading email sending</h2>			<table cellpadding="0" cellspacing="5">				<tr><td><b>Author:</b></td><td>Dave Winer</td></tr>				<tr><td><b>Posted:</b></td><td>7/9/1999; 10:28:03 PM</td></tr>				<tr><td><b>Topic:</b></td><td><a href="msg008379.html">Offloading email sending</a></td></tr>				<tr><td><b>Msg #:</b></td><td>8379</td></tr>				<tr><td><b>Prev/Next:</b></td><td><a href="msg008378.html">8378</a> / <a href="msg008380.html">8380</a></td></tr>				</table>			<br>We've been doing more and more offloading of non-HTTP-server functions from our main server to what was once a spare machine and now is a workhorse on our LAN.<p>

There are two tasks that it's doing for us. First, it reads all the My.UserLand.Com channel files once per hour. It writes them to a folder that the main server checks once a minute. <p>

The second task for the offload server is watching a folder for XML formatted email messages. The XML says who the sender is, who it's going to, a subject, a cc and bcc list, a mimetype, and a body. Here's a simple example:<p>

<pre>
<?xml version="1.0"?>
<mail>
	<recipient>dave@userland.com</recipient>
	<subject>Testing XML-Mail</subject>
	<sender>dave@userland.com</sender>
	<cc>brent@userland.com</cc>
	<bcc>bierman@userland.com,marc@canter.com</bcc>
	<mimeType>text/plain</mimeType>
	<body>Hello World!</body>
	</mail>
</pre><p>

Over on the web server, I stubbed off tcp.sendMail so that instead of sending a mail message, it writes out one of these files in the same folder that the offload server is watching. Here's the routine that my copy of tcp.sendMail is calling:<p>

<pre>
on offloadedMailSender (recipient="", subject="", body="", sender=user.prefs.mailAddress, cc="", bcc="", mimeType="text/plain") &#123;
	local (folder = "h:\\macshare\\xmlMail\\");
	local (xmltext);
	on add (s) &#123;
		xmltext = xmltext + s + "\r"&#125;;
	on clean (s) &#123;
		s = string.replaceall (s, "&", "&amp;");
		s = string.replaceall (s, "<", "<");
		s = string.replaceall (s, ">", "&gt;");
		return (s)&#125;;
	add ("<?xml version=\"1.0"?>");
	add ("<mail>");
	add ("<recipient>" + clean (recipient) + "</recipient>");
	add ("<subject>" + clean (subject) + "</subject>");
	add ("<sender>" + clean (sender) + "</sender>");
	if cc != "" &#123;
		add ("<cc>" + clean (cc) + "</cc>")&#125;;
	if bcc != "" &#123;
		add ("<bcc>" + clean (bcc) + "</bcc>")&#125;;
	add ("<mimeType>" + clean (mimeType) + "</mimeType>");
	add ("<body>" + clean (body) + "</body>");
	add ("</mail>");
	loop &#123; //get a unique name
		local (f = folder + clock.ticks () + ".xml");
		file.surefilepath (f);
		if not file.exists (f) &#123;
			file.writewholefile (f, xmltext);
			break&#125;&#125;&#125;
</pre><p>

Over on the offload server, here's what it's doing every five seconds:<p>

<pre>
local (f);
fileloop (f in config.batchJobServer.prefs.mailFolder, infinity) &#123;
	local (adrlog = log.addToGuestDatabase ("xmlMail"));
	try &#123; //an error in a file doesn't kill the script
		local (xmltext = string (file.readwholefile (f)), xtable);
		file.delete (f);
		local (recipient, subject, body, sender=user.prefs.mailAddress, cc="", bcc="", mimeType="text/plain");
		xml.compile (xmltext, @xtable);
		local (adrmail = xml.getaddress (@xtable, "mail"));
		on getmailvalue (valname) &#123;
			local (s = xml.getvalue (adrmail, valname));
			s = string.replaceall (s, "&amp;lt;", "<");
			s = string.replaceall (s, "&amp;gt;", ">");
			s = string.replaceall (s, "&amp;amp;", "&");
			adrlog^.[valname] = s;
			return (s)&#125;;
		bundle &#123; //get the required values
			recipient = getmailvalue ("recipient");
			subject = getmailvalue ("subject");
			body = getmailvalue ("body")&#125;;
		bundle &#123; //get the optional values
			try &#123;cc = getmailvalue ("cc")&#125;;
			try &#123;bcc = getmailvalue ("bcc")&#125;;
			try &#123;mimeType = getmailvalue ("mimeType")&#125;;
			try &#123;sender = getmailvalue ("sender")&#125;&#125;;
		tcp.sendMail (recipient, subject, body, sender, cc, bcc, mimeType:mimeType, flMessages:true)&#125;
	else &#123;
		adrlog^.error = tryerror&#125;&#125;
</pre><p>

Now when a MailToTheFuture message goes out, or a password confirmation, or a Bulletin, it will flow thru the offload server. So every time you get an automatic email from UserLand, think to yourself <i>"This code is still working."</i>
			<br><br>			<br><br><hr>This page was archived on 6/13/2001; 4:51:21 PM.<br><br>&copy; Copyright 1998-2001 <a href="http://www.userland.com/">UserLand Software</a>, Inc.			</blockquote></blockquote>		</body>	</html>